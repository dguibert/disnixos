{ nixpkgs ? <nixpkgs>
, nixos ? <nixos>
, system ? builtins.currentSystem
, disnix ? builtins.storePath @DISNIX_PREFIX@
, disnixos ? builtins.storePath @prefix@
}:

let
  generateManifestSrc =
    {name, tarball}:
    
    with import nixpkgs { inherit system; };
    
    stdenv.mkDerivation {
      name = "${name}-manifest-src";
      buildCommand =
        ''
          ensureDir $out
          cd $out
          tar xfvj ${tarball}/tarballs/*.tar.bz2 --strip-components=1
        '';
    };
    
    generatePhysicalTestNetwork = networkFile: moduleName:
      with import nixpkgs { inherit system; };
      
      let network = import networkFile;
      in
      stdenv.mkDerivation {
        name = "physical.nix";
        buildCommand =
          ''
            cat > $out <<EOF
            {
              ${lib.concatMapStrings (targetName: "${targetName} = \"${nixos}/${moduleName}\";\n") (builtins.attrNames network)}
            }
            EOF
          '';
      };
in
{
  sourceTarball =
    {name, version, src, officialRelease}:
    
    with import nixpkgs { inherit system; };
    
    releaseTools.sourceTarball {
      inherit name version src officialRelease;
      
      distPhase =
        ''
          mkdir ../$name
          rm -Rf `find . -name .svn`
          mv * ../$name
          cd ..
          ensureDir $out/tarballs
          tar cfvj $out/tarballs/$name.tar.bz2 $name
        '';
    };

  buildManifest = 
    {name, version, tarball, servicesFile, networkFile, externalNetworkFile ? false, distributionFile, externalDistributionFile ? false}:
    
    with import nixpkgs { inherit system; };
    
    releaseTools.nixBuild rec {
      inherit name version;
      src = tarball;
      
      manifestSrc = generateManifestSrc {
        inherit name tarball;
      };
      
      infrastructure = import "${disnixos}/share/disnixos/generate-infrastructure.nix" {
        networkFiles = [ (if externalNetworkFile then networkFile else "${manifestSrc}/${networkFile}") ];
        useVMTesting = true;
        useBackdoor = true;
        inherit nixpkgs nixos;
      };
      
      manifest = import "${disnix}/share/disnix/manifest.nix" {
        servicesFile = "${manifestSrc}/${servicesFile}";
        infrastructureFile = "${infrastructure}";
        distributionFile = if externalDistributionFile then distributionFile else "${manifestSrc}/${distributionFile}";
        targetProperty = "backdoor";
        inherit nixpkgs;
      };
      
      installPhase =
        ''
          ensureDir $out
          ln -s $manifest $out/manifest.xml
        '';

      checkPhase = "echo hello";
    };

  disnixTest =
    {name, manifest, tarball, networkFile, externalNetworkFile ? false, testScript}:
    
    let
      manifestSrc = generateManifestSrc {
        inherit name tarball;
      };
      
      network = if externalNetworkFile then import networkFile else import "${manifestSrc}/${networkFile}";
    in
    with import "${nixos}/lib/testing.nix" { inherit system; };
    with import nixpkgs { inherit system; };
    
    simpleTest {
      nodes = network;
      
      testScript =
        ''
          startAll;

          ${lib.concatMapStrings (targetName:
          ''
            ${"\$"}${targetName}->waitForUnit("network.target");
            ${"\$"}${targetName}->waitForJob("disnix");
            my $pid = ${"\$"}${targetName}->mustSucceed("${socat}/bin/socat tcp-listen:512,fork exec:/bin/sh & echo -n \$!");
            ${"\$"}${targetName}->mustSucceed("while [ \"\$(ps -p $pid | grep socat)\" = \"\" ]; do sleep 0.5; done");
    
          '') (builtins.attrNames network)}

          ${"\$"}${builtins.head (builtins.attrNames network)}->mustSucceed("${disnix}/bin/disnix-activate --interface ${disnixos}/bin/disnix-backdoor-client --no-coordinator-profile --no-lock ${manifest}/manifest.xml");
  
          ${testScript}
        '';
    };
}
